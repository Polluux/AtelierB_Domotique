/* Superviseur_r
 * Author: 
 * Creation date: 13/12/18
 */

REFINEMENT Superviseur_r
REFINES Superviseur

VARIABLES
    obj                  //L'ensemble des objets gérés par notre superviseur
    ,incompatibilites    //La relation qui associe un objet avec lesquels il n'est pas compatible
    ,etat                //La fonction qui à un objet associe son état
    ,type                //La fonction qui à un objet associe son type
    ,pos                 //La fonction qui à un objet associe un couple de valeurs de position x et y
    ,alarme              //La variable boolean qui définie si l'alarme est enclenchée ou non
   

INITIALISATION
    obj                    :=    {}
    || incompatibilites    :=    {}
    || etat                :=    {}
    || type                :=    {}
    || pos                 :=    {}
    || alarme              :=    FALSE


OPERATIONS
    addObjetImmobile(oo)=
    PRE
        oo : OBJETS
        & oo /: obj
    THEN
        obj := obj \/ {oo}
        || etat := etat \/ {oo |-> inactif}
        || type := type \/ {oo |-> immobile}
    END;

    
    addObjetMobile(oo,xx,yy)=
    PRE
        oo : OBJETS
        & oo /: obj
        & xx : NAT
        & yy : NAT
        & pos~[{{xx|->yy}}] = {}
    THEN
        obj := obj \/ {oo}
        || etat := etat \/ {oo |-> inactif}
        || type := type \/ {oo |-> mobile}
        || pos := pos \/ {oo |-> {xx |-> yy}}
    END;


    activate(oo)=
    PRE
        oo : obj
        & etat(oo) /= actif
        & (incompatibilites[{oo}] /\ etat~[{actif}] = {})
        & (incompatibilites~[{oo}] /\ etat~[{actif}] = {})
        & alarme = FALSE
    THEN
        etat(oo) := actif
    END;

    
    deactivate(oo)=
    PRE
        oo : obj
        & etat(oo) = actif
    THEN
        etat(oo) := inactif
    END;

    
    activateAlarm=
    PRE
        alarme = FALSE
    THEN
        etat := (etat|>{invalide} \/ etat|>{inactif} \/ etat~[{actif}]*{inactif})
        || alarme := TRUE
    END;

    
    rr<--getActifObjects=
    BEGIN
        rr := etat~[{actif}]
    END;

    
    rr<--getInvalideObjects=
    BEGIN
        rr := etat~[{invalide}]
    END

 END